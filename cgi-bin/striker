#!/usr/bin/perl
#
# Striker - The Anvil! Cluster Dashboard
# 
# This software is released under the GNU GPL v2+ license.
# 
# No warranty is provided. Do not use this software unless you are willing and able to take full liability 
# for it's use. The authors take care to prevent unexpected side effects when using this program. However, no
# software is perfect and bugs may exist which could lead to hangs or crashes in the program, in your cluster
# and possibly even data loss.
# 
# If you are concerned about these risks, please stick to command line tools.
# 
# This program is designed to extend clusters built according to this tutorial:
# - https://alteeve.ca/w/AN!Cluster_Tutorial_2
#
# This program's source code and updates are available on Github:
# - https://github.com/ClusterLabs/striker
#
# Author;
# Alteeve's Niche!  -  https://alteeve.ca
# Madison Kelly     -  mkelly@alteeve.ca
# 
# TODO:
# - Make it so that the backup from one Striker can be loaded into another without changing it's host 
#   name/IPs/etc. A "Don't alter system" checkbox?
# - Adding a VM that is running on the "wrong" node gets it added to the wrong failover domain.
# - Check to see if a VM is running outside of clustat and, if so, auto-enable it on the appropriate node.
# - Add a "Cancel" button to each "Confirm" box.
# - Add an ability to trigger a status email. SIGUSR1 via script -> email Allow users to rename VMs.
# - Add 'virsh undefine' when deleting VMs if 'virsh list --all' shows it still defined on one of the VMs.
# - Build a VM outside of the cluster and sort out a way to have the dashboard insert it into the cluster.
# - Make "Node -> Offline" be a "Warning!"
# - Make "Bond -> Down" be a warning
# - Enable an administrative LOCK OUT switch to prevent reboots during things like firmware updates.
# - Add ability to "unplug" a server's network cable.
#   
# - Pass a timestamp with all CGI commands for five minutes in the future and abort any actions that are 
#   older than that.
# - Pass $THIS_FILE and __LINE__ on all 'error()' calls.
# 
# BUG:
# - [root@rm-a01n01 ~]# virsh destroy vm07-newmas
#   error: Failed to destroy domain vm07-newmas
#   error: operation failed: failed to kill qemu process with SIGTERM
#   ^- qemu fails to respond to virsh, possibly because of high load on host. No current known fix except to
#      migrate other servers to peer and fence the node.

use strict;
use warnings;
use lib 'lib';

my $THIS_FILE = "striker";
use AN::Common;
use AN::Cluster;
use AN::Striker;
use AN::InstallManifest;
use AN::Tools;

# Turn off buffering so that the pinwheel will display while waiting for the SSH call(s) to complete.
$| = 1;

my ($an) = AN::Common::initialize($THIS_FILE, 1);
$an->default_log_file    ($an->data->{path}{log_file});
$an->default_log_language($an->data->{sys}{log_language});
$an->Log->level          ($an->data->{sys}{log_level});

# Set some defaults
$an->default_language    ($an->data->{scancore}{language});
$an->default_log_language($an->data->{scancore}{log_language});
$an->default_log_file    ($an->data->{path}{log_file});

# Read my stuff
$an->Storage->read_conf({file => $an->data->{path}{striker_config}});
$an->Storage->read_words({file => $an->data->{path}{scancore_strings}});
$an->Storage->read_words({file => $an->data->{path}{striker_strings}});

# Log our startup
my ($date, $time) = $an->Get->date_and_time();
$an->Log->entry({log_level => 1, message_key => "log_0001", message_variables => {
	date	=>	$date, 
	'time'	=>	$time,
}, file => $THIS_FILE, line => __LINE__});

# Get (create if needed) my UUID.
$an->Storage->prep_local_uuid($an);

# Connect to the databases.
$an->data->{sys}{db_connections} = $an->DB->connect_to_databases({
	file	=>	$THIS_FILE,
	quiet	=>	1
});

# Read in some configuration information
$an->Storage->read_hosts();
$an->Storage->read_ssh_config();

# Read in our Anvil! systems. If only one anvil is found, it will be auto-set in 'cgi::anvil'.
$an->ScanCore->parse_anvil_data();

# Check for passed-in CGI variables.
$an->Web->check_all_cgi();

# Pick up the list of volume groups.
if ($an->data->{cgi}{vg_list})
{
	my @more_vars;
	foreach my $vg (split/,/, $an->data->{cgi}{vg_list})
	{
		next if not $vg;
		push @more_vars, "vg_$vg";
		push @more_vars, "vg_suffix_$vg";
	}
	$an->Web->get_cgi({variables => \@more_vars});
}
# Pick up the list of optical devices.
if ($an->data->{cgi}{device_keys})
{
	my @more_vars;
	foreach my $device (split/,/, $an->data->{cgi}{device_keys})
	{
		next if not $device;
		push @more_vars, "$device";
		my $drive = ($device =~ /media_(.*)/)[0];
		push @more_vars, "insert_$drive";
	}
	$an->Web->get_cgi({variables => \@more_vars});
}

# Print the header.
AN::Cluster::header($an);

# Do I have a cluster name?
if ($an->data->{cgi}{logo})
{
	if ($an->data->{cgi}{config})
	{
		$an->Striker->configure();
		AN::Cluster::configure_dashboard($an);
	}
	else
	{
		# This is also where we provide options for backup/restore, install manifests, etc.
		AN::Cluster::show_anvil_selection_and_striker_options($an);
	}
}
elsif ($an->data->{cgi}{config})
{
	AN::Cluster::configure_dashboard($an);
}
elsif ($an->data->{cgi}{'system'})
{
	AN::Cluster::configure_local_system($an);
}
elsif ($an->data->{cgi}{anvil_uuid})
{
	$an->Striker->load_anvil();
	$an->Striker->scan_anvil();
	
	die "$THIS_FILE ".__LINE__."; testing...\n";
	#AN::Cluster::scan_cluster($an);
	#AN::Striker::display_details($an);
}
elsif ($an->data->{cgi}{cluster})
{
	my $cluster                                  = $an->data->{cgi}{cluster};
	   $an->data->{sys}{root_password}           = $an->data->{clusters}{$cluster}{root_pw};
	   $an->data->{clusters}{$cluster}{ricci_pw} = $an->data->{clusters}{$cluster}{root_pw} if not $an->data->{clusters}{$cluster}{ricci_pw};
	$an->Log->entry({log_level => 4, message_key => "an_variables_0004", message_variables => {
		name1 => "cluster",                        value1 => $cluster,
		name2 => "clusters::${cluster}::root_pw",  value2 => $an->data->{clusters}{$cluster}{root_pw},
		name3 => "clusters::${cluster}::ricci_pw", value3 => $an->data->{clusters}{$cluster}{ricci_pw},
		name4 => "sys::root_password",             value4 => $an->data->{sys}{root_password},
	}, file => $THIS_FILE, line => __LINE__});
	
	# If the requested cluster isn't found in the config file, error out.
	if (not $an->data->{clusters}{$cluster}{nodes})
	{
		# You can pass in 'anvil => $cluster' as the first hash reference in the 'template' call and 
		# then use the string directly in the template, but I think that's too indirect for future
		# devs to follow initially.
		my $say_message = $an->String->get({key => "message_0003", variables => { anvil => $cluster }});
		print $an->Web->template({file => "main-page.html", template => "no-anvil-found", replace => { message => $say_message }});
	}
	else
	{
		if ($an->data->{cgi}{task})
		{
			$an->data->{sys}{show_refresh} = 0;
			AN::Striker::process_task($an);
		}
		else
		{
			# Set the node list.
			$an->Log->entry({log_level => 3, message_key => "an_variables_0002", message_variables => {
				name1 => "cluster", value1 => $cluster,
				name2 => "ricci_pw", value2 => $an->data->{clusters}{$cluster}{ricci_pw},
			}, file => $THIS_FILE, line => __LINE__});
			$an->data->{nodes} = $an->data->{clusters}{$cluster}{nodes};
			AN::Cluster::scan_cluster($an);
			AN::Striker::display_details($an);
		}
	}
}
else
{
	# Ask the user which cluster they want to look at.
	AN::Cluster::show_anvil_selection_and_striker_options($an);
}
AN::Cluster::footer($an);

exit (0);
