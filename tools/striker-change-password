#!/usr/bin/perl
#
# This program can change the password of the local Striker dashboard (and update the nodes that talk to 
# it's ScanCore database). It can also change the password of an Anvil!.
# 
# Return Codes:
# 0  = OK
# 1  = Not run as root.
# 2  = Not run on a Striker dashboard.
# 3  = No password given
# 4  = Neither 'local' or 'anvil' set.
# 5  = Could not connect to any ScanCore database.
# 6  = Password was too short.
# 7  = One or both nodes offline for an Anvil! password change request.
# 8  = The requested Anvil! was not found.
# 9  = Failed to connect to our own database with --local being requested.
# 10 = One or more nodes are inaccessible and '--force' not used.
# 11 = One or more Striker peers are inaccessible and '--force' not used.
# 12 = One of the Anvil! nodes is offline, which can't be forced when changing an Anvil!'s password.
# 13 = 'fence_check' failed after changing the password on a node's IPMI.
# 14 = Checking fencing returned an unexpected return code.
# 

use strict;
use warnings;
use IO::Handle;
use AN::Tools;

# Turn off buffering.
$| = 1;

# Strip the 'PATH' variable down so that it doesn't cause problems when called
# via a setuid wrapper.
$ENV{'PATH'} = "/usr/local/sbin:/usr/local/bin:/sbin:/bin:/usr/sbin:/usr/bin";

# Prevent a discrepency between UID/GID and EUID/EGID from throwing an error.
$< = $>;
$( = $);

# Figure out who and where I am.
my $program_name        =  $0;
my $THIS_FILE           =  ($program_name =~ /^.*\/(.*)$/)[0];
my $running_directory   =  ($program_name =~ /^(.*?)\/$THIS_FILE$/)[0];
if (($running_directory =~ /^\./) && ($ENV{PWD}))
{
	$running_directory =~ s/^\./$ENV{PWD}/;
}

my $an = AN::Tools->new({
	data		=> {
		anvil		=>	{
			name		=>	"",
			uuid		=>	"",
		},
		password	=>	{
			'truncate'	=>	{
				ipmi		=>	16,
			},
			minimum_length	=>	6,
			to_set		=>	{
				apache		=>	"",
				'cluster.conf'	=>	"",
				ipmi		=>	"",
				scancore	=>	"",
				'striker.conf'	=>	"",
				'system'	=>	"",
			},
		},
		sys		=>	{
			log_level	=>	2,
			new_password	=>	"",
		},
	},
});

# Initialize.
$an->default_language    ($an->data->{sys}{language});
$an->default_log_language($an->data->{sys}{log_language});
$an->default_log_file    ($an->data->{path}{log_file});
$an->Log->level          ($an->data->{sys}{log_level});
$an->Storage->read_conf  ({file => $an->data->{path}{striker_config}});
$an->Storage->read_words ({file => $an->data->{path}{striker_strings}});
$an->Get->switches();
$an->Log->db_transactions(1) if $an->data->{'anvil-safe-start'}{log_db_transactions};
$an->Readable->base2(1);

if (($an->data->{switches}{h})   or 
    ($an->data->{switches}{'?'}) or 
    ($an->data->{switches}{help}))
{
	print_usage($an);
	$an->nice_exit({exit_code => 0});
}

# Make sure we're running as 'root'
# $< == real UID, $> == effective UID
if (($< != 0) && ($> != 0))
{
	# Not root
	print $an->String->get({key => "scp_error_0001"})."\n";
	$an->nice_exit({exit_code => 1});
}

if ((-e $an->data->{path}{cluster_conf}) or (not -e $an->data->{path}{htpasswd}))
{
	# Not a dashboard
	print $an->String->get({key => "scp_error_0002"})."\n";
	$an->nice_exit({exit_code => 2});
}

if ((not $an->data->{switches}{password}) or ($an->data->{switches}{password} eq "#!SET!#"))
{
	# No password...
	print $an->String->get({key => "scp_error_0003"})."\n";
	$an->nice_exit({exit_code => 3});
}

# Prepare the passwords and ask the user to confirm.
prepare_passwords($an);

# Can I connect to a database?
my $connections = $an->DB->connect_to_databases({file => $THIS_FILE});
$an->Log->entry({log_level => 3, message_key => "an_variables_0001", message_variables => {
	name1 => "connections", value1 => $connections, 
}, file => $THIS_FILE, line => __LINE__});
if (not $connections)
{
	# No databases...
	print $an->String->get({key => "scp_error_0005"})."\n";
	$an->nice_exit({exit_code => 5});
}

# How are we running?
if ($an->data->{switches}{'local'})
{
	# Before we do anything, check if I failed to connect to my database. This can't be forced past.
	if (not $an->data->{sys}{local_db_id})
	{
		print $an->String->get({key => "scp_error_0009"})."\n";
		$an->nice_exit({exit_code => 9});
	}
	
	print $an->String->get({key => "scp_message_0004"})."\n";
	print $an->String->get({key => "scp_message_0011"})."\n";
	my $nodes_offline = check_all_nodes($an);
	my $peers_offline = check_all_peers($an);	# We don't care about offline Strikers at this point.
	if ((($nodes_offline) or ($peers_offline)) && ($an->data->{switches}{force}))
	{
		# Force requested.
		print $an->String->get({key => "scp_message_0003"})."\n";
	}
	elsif ($nodes_offline)
	{
		print $an->String->get({key => "scp_error_0010"})."\n";
		$an->nice_exit({exit_code => 10});
	}
	elsif ($peers_offline)
	{
		print $an->String->get({key => "scp_error_0011"})."\n";
		$an->nice_exit({exit_code => 11});
	}
}
elsif (($an->data->{switches}{anvil}) && ($an->data->{switches}{anvil} ne "#!SET!#"))
{
	# Make sure we know about this anvil. It will error out if the Anvil! isn't found.
	print $an->String->get({key => "scp_warning_0010"})."\n";
	print $an->String->get({key => "scp_message_0012", variables => { anvil_name => $an->data->{switches}{anvil} }})."\n";
	print $an->String->get({key => "scp_message_0011"})."\n";
	find_anvil($an);
}
else
{
	print $an->String->get({key => "scp_error_0004"})."\n";
	$an->nice_exit({exit_code => 4});
}

# Finally, ask the user to confirm
if (($an->data->{switches}{y}) or ($an->data->{switches}{yes}))
{
	print $an->String->get({key => "scp_message_0024"})."\n";
}
else
{
	print $an->String->get({key => "scp_message_0005", variables => { password => $an->data->{switches}{password} }});
	my $answer = <STDIN>;
	chomp($answer);
	if (($answer) && (lc($answer) =~ /^y/))
	{
		# Proceed!
		print $an->String->get({key => "scp_message_0006"})."\n";
	}
	else
	{
		# Eject, eject!
		print $an->String->get({key => "scp_message_0007"})."\n";
		$an->nice_exit({exit_code => 0});
	}
}
print "\n";

# Still alive? Proceed!
if ($an->data->{switches}{'local'})
{
	change_local_passwords($an);
}
else
{
	change_anvil_passwords($an);
}


$an->nice_exit({exit_code => 0});


#############################################################################################################
# Functions                                                                                                 #
#############################################################################################################

# This changes the passwords on an Anvil! system.
sub change_anvil_passwords
{
	my ($an) = @_;
	$an->Log->entry({log_level => 3, title_key => "tools_log_0001", title_variables => { function => "change_anvil_passwords" }, message_key => "tools_log_0002", file => $THIS_FILE, line => __LINE__});
	
	# Make sure both nodes are still online
	my $ok = verify_anvil_access($an);
	if (not $ok)
	{
		# Bail out!
		print $an->String->get({key => "scp_error_0012"})."\n";
		$an->nice_exit({exit_code => 12});
	}
	
	### NOTE: This method will set 'ipmi::<node>::ipmi_user' if found in cluster.conf.
	# Change the cluster.conf on node 1 and push it out.
	my $cman_rc = $an->Cman->update_cluster_conf({
		anvil_uuid   => $an->data->{anvil}{uuid}, 
		task         => "fence",
		subtask      => "password",
		method       => "ipmi",
		new_password => $an->data->{password}{to_set}{'cluster.conf'},
	});
	
	# Change the node's IPMI
	foreach my $node_key ("node1", "node2")
	{
		my $this_node = $an->data->{sys}{anvil}{$node_key}{name};
		my $ipmi_user = $an->data->{ipmi}{$this_node}{ipmi_user} ? $an->data->{ipmi}{$this_node}{ipmi_user} : "";
		$an->Log->entry({log_level => 3, message_key => "an_variables_0002", message_variables => {
			name1 => "this_node", value1 => $this_node, 
			name2 => "ipmi_user", value2 => $ipmi_user, 
		}, file => $THIS_FILE, line => __LINE__});
		
		print $an->String->get({key => "scp_message_0013", variables => { node_name => $this_node }});
		if ($ipmi_user)
		{
			my $ipmi_rc = $an->System->configure_ipmi({
				task         => "password", 
				ipmi_user    => $ipmi_user, 
				new_password => $an->data->{password}{to_set}{ipmi}, 
				target       => $an->data->{sys}{anvil}{$node_key}{use_ip}, 
				port         => $an->data->{sys}{anvil}{$node_key}{use_port}, 
				password     => $an->data->{sys}{anvil}{$node_key}{use_password}, 
			});
			$an->Log->entry({log_level => 3, message_key => "an_variables_0001", message_variables => {
				name1 => "ipmi_rc", value1 => $ipmi_rc, 
			}, file => $THIS_FILE, line => __LINE__});
			if ($ipmi_rc eq "2")
			{
				# No IPMI
				print $an->String->get({key => "state_0019"})."\n";
			}
			elsif ($ipmi_rc eq "3")
			{
				# LAN channel not found.
				print $an->String->get({key => "log_0127"})."\n";
			}
			elsif ($ipmi_rc eq "4")
			{
				# User ID not found.
				print $an->String->get({key => "log_0129"})."\n";
			}
			elsif ($ipmi_rc)
			{
				# What?
				print $an->String->get({key => "scp_warning_0004", variables => { rc => $ipmi_rc }})."\n";
			}
			else
			{
				# 0 == goood.
				print $an->String->get({key => "state_0073"})."\n";
			}
		}
		else
		{
			# Don't know who the IPMI user is, so probably no IPMI.
			print $an->String->get({key => "scp_message_0025"})."\n";
		}
	}
	
	# Run a 'fence_check' to make sure the new password is working properly.
	foreach my $node_key ("node1", "node2")
	{
		my $this_node = $an->data->{sys}{anvil}{$node_key}{name};
		$an->Log->entry({log_level => 3, message_key => "an_variables_0001", message_variables => {
			name1 => "this_node", value1 => $this_node, 
		}, file => $THIS_FILE, line => __LINE__});
		
		print $an->String->get({key => "scp_message_0026", variables => { node_name => $this_node }});
		my $fence_rc = $an->Cman->check_fencing({
			target   => $an->data->{sys}{anvil}{$node_key}{use_ip}, 
			port     => $an->data->{sys}{anvil}{$node_key}{use_port}, 
			password => $an->data->{sys}{anvil}{$node_key}{use_password}, 
		});
		$an->Log->entry({log_level => 3, message_key => "an_variables_0001", message_variables => {
			name1 => "fence_rc", value1 => $fence_rc, 
		}, file => $THIS_FILE, line => __LINE__});
		
		if ($fence_rc eq "2")
		{
			# Didn't run
			print $an->String->get({key => "scp_warning_0009", variables => { rc => $fence_rc }})."\n";
		}
		elsif ($fence_rc eq "1")
		{
			# Failed!
			print $an->String->get({key => "scp_error_0013", variables => { node => $this_node }})."\n";
			$an->nice_exit({exit_code => 13});
		}
		elsif ($fence_rc eq "0")
		{
			# Success!
			print $an->String->get({key => "state_0005"})."\n";
		}
		else
		{
			# Huh?
			print $an->String->get({key => "scp_error_0014", variables => { 
				node => $this_node, 
				rc   => $fence_rc, 
			}})."\n";
			$an->nice_exit({exit_code => 14});
		}
	}
	
	# Now update the nodes...
	foreach my $node_key ("node1", "node2")
	{
		my $this_node = $an->data->{sys}{anvil}{$node_key}{name};
		my $node_uuid = $an->data->{sys}{anvil}{$node_key}{uuid};
		$an->Log->entry({log_level => 3, message_key => "an_variables_0002", message_variables => {
			name1 => "this_node", value1 => $this_node, 
			name2 => "node_uuid", value2 => $node_uuid, 
		}, file => $THIS_FILE, line => __LINE__});
		
		print $an->String->get({key => "scp_message_0027", variables => { node_name => $this_node }})."\n";
		foreach my $user ("ricci", "root")
		{
			print $an->String->get({key => "scp_message_0028", variables => { user => $user }});
			my $user_rc = $an->System->change_shell_user_password({
				user         => $user, 
				new_password => $an->data->{password}{to_set}{'system'}, 
				target       => $an->data->{sys}{anvil}{$node_key}{use_ip}, 
				port         => $an->data->{sys}{anvil}{$node_key}{use_port}, 
				password     => $an->data->{sys}{anvil}{$node_key}{use_password}, 
			});
			$an->Log->entry({log_level => 3, message_key => "an_variables_0001", message_variables => {
				name1 => "user_rc", value1 => $user_rc, 
			}, file => $THIS_FILE, line => __LINE__});
			if ($user_rc)
			{
				# What?
				print $an->String->get({key => "scp_warning_0004", variables => { rc => $user_rc }})."\n";
			}
			else
			{
				# 0 == goood.
				print $an->String->get({key => "state_0073"})."\n";
			}
		}
		
		# Update the ScanCore database for this node
		print $an->String->get({key => "scp_message_0029", variables => { node_name => $this_node }});
		my $query = "
UPDATE 
    nodes 
SET 
    node_password = ".$an->data->{sys}{use_db_fh}->quote($an->data->{password}{to_set}{'system'}).", 
    modified_date = ".$an->data->{sys}{use_db_fh}->quote($an->data->{sys}{db_timestamp})." 
WHERE 
    node_uuid = ".$an->data->{sys}{use_db_fh}->quote($node_uuid)."
;";
		$an->Log->entry({log_level => 3, message_key => "an_variables_0001", message_variables => {
			name1 => "query", value1 => $query, 
		}, file => $THIS_FILE, line => __LINE__});
		$an->DB->do_db_write({query => $query, source => $THIS_FILE, line => __LINE__});
		print $an->String->get({key => "state_0073"})."\n";
	}
	
	# Update the ScanCore database for this Anvil!
	print $an->String->get({key => "scp_message_0030", variables => { anvil_name => $an->data->{sys}{anvil}{name} }});
	my $query = "
UPDATE 
    anvils 
SET 
    anvil_password = ".$an->data->{sys}{use_db_fh}->quote($an->data->{password}{to_set}{'system'}).", 
    modified_date  = ".$an->data->{sys}{use_db_fh}->quote($an->data->{sys}{db_timestamp})." 
WHERE 
    anvil_uuid     = ".$an->data->{sys}{use_db_fh}->quote($an->data->{sys}{anvil}{uuid})."
;";
	$an->Log->entry({log_level => 3, message_key => "an_variables_0001", message_variables => {
		name1 => "query", value1 => $query, 
	}, file => $THIS_FILE, line => __LINE__});
	$an->DB->do_db_write({query => $query, source => $THIS_FILE, line => __LINE__});
	print $an->String->get({key => "state_0073"})."\n";
	
	# Done!
	print $an->String->get({key => "scp_message_0031"})."\n";
	
	return(0);
}

# This checks that both nodes are still accessible.
sub verify_anvil_access
{
	my ($an) = @_;
	$an->Log->entry({log_level => 3, title_key => "tools_log_0001", title_variables => { function => "verify_anvil_access" }, message_key => "tools_log_0002", file => $THIS_FILE, line => __LINE__});
	
	my $ok              = 1;
	my $this_anvil_uuid = $an->data->{anvil}{uuid};
	my $this_anvil_name = $an->data->{anvil}{name};
	$an->Log->entry({log_level => 3, message_key => "an_variables_0002", message_variables => {
		name1 => "this_anvil_uuid", value1 => $this_anvil_uuid, 
		name2 => "this_anvil_name", value2 => $this_anvil_name, 
	}, file => $THIS_FILE, line => __LINE__});
	
	# Load this Anvil!. It should already be loaded, but lets be safe.
	$an->Striker->load_anvil({anvil_uuid => $this_anvil_uuid});
	
	# Quick scan both nodes to make sure we have access.
	foreach my $node_key ("node1", "node2")
	{
		# Make sure we can still access it.
		$an->Striker->scan_node({uuid => $an->data->{sys}{anvil}{$node_key}{uuid}, short_scan => 1});
		
		# Make sure the node is still online.
		if (not $an->data->{sys}{anvil}{$node_key}{online})
		{
			# Failed. :(
			print $an->String->get({key => "scp_warning_0008", variables => { node_name => $an->data->{sys}{anvil}{$node_key}{name} }})."\n";
			$ok = 0;
			$an->Log->entry({log_level => 3, message_key => "an_variables_0001", message_variables => {
				name1 => "ok", value1 => $ok, 
			}, file => $THIS_FILE, line => __LINE__});
		}
	}
	
	$an->Log->entry({log_level => 3, message_key => "an_variables_0001", message_variables => {
		name1 => "ok", value1 => $ok, 
	}, file => $THIS_FILE, line => __LINE__});
	return($ok);
}

# This changes the local passwords and then updates the password on the nodes so that they have our new 
# password, too.
sub change_local_passwords
{
	my ($an) = @_;
	$an->Log->entry({log_level => 3, title_key => "tools_log_0001", title_variables => { function => "change_local_passwords" }, message_key => "tools_log_0002", file => $THIS_FILE, line => __LINE__});
	
	# We'll keep track of any nodes that went offline (or were offline from the start).
	my $offline = {};
	
	### NOTE: This method sanitizes the passwords.
	# Update the local 'root' user.
	print $an->String->get({key => "scp_message_0014"});
	my $root_rc = $an->System->change_shell_user_password({
		user         => "root", 
		new_password => $an->data->{password}{to_set}{'system'}
	});
	$an->Log->entry({log_level => 3, message_key => "an_variables_0001", message_variables => {
		name1 => "root_rc", value1 => $root_rc, 
	}, file => $THIS_FILE, line => __LINE__});
	if ($root_rc)
	{
		# What?
		print $an->String->get({key => "scp_warning_0004", variables => { rc => $root_rc }})."\n";
	}
	else
	{
		# 0 == goood.
		print $an->String->get({key => "state_0073"})."\n";
	}
	
	# Update apache
	my $changed_users = $an->System->change_apache_password({new_password => $an->data->{password}{to_set}{apache}});
	foreach my $user (sort {$a cmp $b} keys %{$changed_users})
	{
		print $an->String->get({key => "scp_message_0016", variables => { user => $user }});
		my $apache_rc = $changed_users->{$user};
		$an->Log->entry({log_level => 3, message_key => "an_variables_0001", message_variables => {
			name1 => "apache_rc", value1 => $apache_rc, 
		}, file => $THIS_FILE, line => __LINE__});
		if ($apache_rc)
		{
			# What?
			print $an->String->get({key => "scp_warning_0004", variables => { rc => $apache_rc }})."\n";
		}
		else
		{
			# 0 == goood.
			print $an->String->get({key => "state_0073"})."\n";
		}
	}
	
	# Update the scancore postgresql password
	print $an->String->get({key => "scp_message_0017"});
	my $db_id = $an->data->{sys}{local_db_id};
	$an->Log->entry({log_level => 3, message_key => "an_variables_0001", message_variables => {
		name1 => "scancore::db::${db_id}::user", value1 => $an->data->{scancore}{db}{$db_id}{user}, 
	}, file => $THIS_FILE, line => __LINE__});
	
	my $pgsql_rc = $an->System->change_postgresql_password({
		user         => $an->data->{scancore}{db}{$db_id}{user}, 
		new_password => $an->data->{password}{to_set}{scancore}
	});
	$an->Log->entry({log_level => 3, message_key => "an_variables_0001", message_variables => {
		name1 => "pgsql_rc", value1 => $pgsql_rc, 
	}, file => $THIS_FILE, line => __LINE__});
	if ($pgsql_rc)
	{
		# What?
		print $an->String->get({key => "scp_warning_0004", variables => { rc => $pgsql_rc }})."\n";
	}
	else
	{
		# 0 == goood.
		print $an->String->get({key => "state_0073"})."\n";
	}
	
	# Update IPMI (if applicable), rc = 2 == no IPMI
	if ($an->data->{switches}{'ipmi-user'})
	{
		print $an->String->get({key => "scp_message_0018"});
		my $ipmi_rc = $an->System->configure_ipmi({
			task         => "password", 
			ipmi_user    => $an->data->{switches}{'ipmi-user'}, 
			new_password => $an->data->{password}{to_set}{ipmi},
		});
		$an->Log->entry({log_level => 3, message_key => "an_variables_0001", message_variables => {
			name1 => "ipmi_rc", value1 => $ipmi_rc, 
		}, file => $THIS_FILE, line => __LINE__});
		if ($ipmi_rc eq "2")
		{
			# No IPMI
			print $an->String->get({key => "state_0019"})."\n";
		}
		elsif ($ipmi_rc eq "3")
		{
			# LAN channel not found.
			print $an->String->get({key => "log_0127"})."\n";
		}
		elsif ($ipmi_rc eq "4")
		{
			# User ID not found.
			print $an->String->get({key => "log_0129"})."\n";
		}
		elsif ($ipmi_rc)
		{
			# What?
			print $an->String->get({key => "scp_warning_0004", variables => { rc => $pgsql_rc }})."\n";
		}
		else
		{
			# 0 == goood.
			print $an->String->get({key => "state_0073"})."\n";
		}
	}
	
	# Update the local striker.conf
	print $an->String->get({key => "scp_message_0015"})."\n";
	print $an->String->get({key => "scp_message_0019"});
	my $striker_rc = $an->Striker->update_striker_conf({
		variable => "scancore::db::${db_id}::password",
		value    => $an->data->{password}{to_set}{'striker.conf'},
	});
	$an->Log->entry({log_level => 3, message_key => "an_variables_0001", message_variables => {
		name1 => "striker_rc", value1 => $striker_rc, 
	}, file => $THIS_FILE, line => __LINE__});
	if ($striker_rc)
	{
		# What?
		print $an->String->get({key => "scp_warning_0004", variables => { rc => $pgsql_rc }})."\n";
	}
	else
	{
		# 0 == goood.
		print $an->String->get({key => "state_0073"})."\n";
	}
	
	# Now update the striker.conf of any peer dashboards.
	my $possible_hosts = $an->Striker->build_local_host_list();
	foreach my $peer (sort {$a cmp $b} keys %{$an->data->{sys}{dashboard}})
	{
		$an->Log->entry({log_level => 3, message_key => "an_variables_0001", message_variables => {
			name1 => "sys::dashboard::${peer}::online", value1 => $an->data->{sys}{dashboard}{$peer}{online}, 
		}, file => $THIS_FILE, line => __LINE__});
		if ($an->data->{sys}{dashboard}{$peer}{online})
		{
			my $target   = $an->data->{sys}{dashboard}{$peer}{use_ip};
			my $port     = $an->data->{sys}{dashboard}{$peer}{use_port}; 
			my $password = $an->data->{sys}{dashboard}{$peer}{password}; 
			$an->Log->entry({log_level => 3, message_key => "an_variables_0002", message_variables => {
				name1 => "target", value1 => $target, 
				name2 => "port",   value2 => $port, 
			}, file => $THIS_FILE, line => __LINE__});
			$an->Log->entry({log_level => 4, message_key => "an_variables_0001", message_variables => {
				name1 => "password", value1 => $password, 
			}, file => $THIS_FILE, line => __LINE__});
			
			# Get our DB ID from the peer.
			my $peer_db_id = $an->Striker->get_db_id_from_striker_conf({
				hosts    => $possible_hosts,
				target   => $target, 
				port     => $port, 
				password => $password, 
			});
				$an->Log->entry({log_level => 3, message_key => "an_variables_0001", message_variables => {
					name1 => "peer_db_id", value1 => $peer_db_id, 
				}, file => $THIS_FILE, line => __LINE__});
			
			if ($peer_db_id =~ /^\d+$/)
			{
				print $an->String->get({key => "scp_message_0020", variables => { peer_name => $target }});
				my $striker_rc = $an->Striker->update_striker_conf({
					variable => "scancore::db::${peer_db_id}::password",
					value    => $an->data->{password}{to_set}{'striker.conf'},
					target   => $target, 
					port     => $port, 
					password => $password, 
				});
				if ($striker_rc)
				{
					# What?
					print $an->String->get({key => "scp_warning_0004", variables => { rc => $pgsql_rc }})."\n";
				}
				else
				{
					# 0 == goood.
					print $an->String->get({key => "state_0073"})."\n";
				}
			}
			else
			{
				# Failed to find our DB ID.
				print $an->String->get({key => "scp_warning_0007", variables => { rc => $pgsql_rc }})."\n";
			}
		}
		else
		{
			# Remind the user that this dashboard is offline.
			print $an->String->get({key => "scp_warning_0005", variables => { peer => $peer }})."\n";
		}
	}
	
	# Now update the striker.conf on all available nodes.
	foreach my $hash_ref (@{$an->data->{anvil_data}})
	{
		my $this_anvil_uuid = $hash_ref->{anvil_uuid};
		my $this_anvil_name = $hash_ref->{anvil_name};
		$an->Log->entry({log_level => 3, message_key => "an_variables_0002", message_variables => {
			name1 => "this_anvil_uuid", value1 => $this_anvil_uuid, 
			name2 => "this_anvil_name", value2 => $this_anvil_name, 
		}, file => $THIS_FILE, line => __LINE__});
		
		print $an->String->get({key => "scp_message_0021", variables => { anvil_name => $this_anvil_name }})."\n";
		
		# Load this Anvil!
		$an->Striker->load_anvil({anvil_uuid => $this_anvil_uuid});
		
		# Change each node's striker.conf
		foreach my $node_key ("node1", "node2")
		{
			# If either node is offline, error out. We don't care about '--force' here because 
			# they've asked us to change the passwords on an Anvil!, which requires both nodes 
			# be up.
			$an->Log->entry({log_level => 3, message_key => "an_variables_0001", message_variables => {
				name1 => "sys::anvil::${node_key}::online", value1 => $an->data->{sys}{anvil}{$node_key}{online}, 
			}, file => $THIS_FILE, line => __LINE__});
			if ($an->data->{sys}{anvil}{$node_key}{online})
			{
				my $target   = $an->data->{sys}{anvil}{$node_key}{use_ip};
				my $port     = $an->data->{sys}{anvil}{$node_key}{use_port}; 
				my $password = $an->data->{sys}{anvil}{$node_key}{password}; 
				$an->Log->entry({log_level => 3, message_key => "an_variables_0002", message_variables => {
					name1 => "target", value1 => $target, 
					name2 => "port",   value2 => $port, 
				}, file => $THIS_FILE, line => __LINE__});
				$an->Log->entry({log_level => 4, message_key => "an_variables_0001", message_variables => {
					name1 => "password", value1 => $password, 
				}, file => $THIS_FILE, line => __LINE__});
				
				# Get our DB ID from the peer.
				my $node_db_id = $an->Striker->get_db_id_from_striker_conf({
					hosts    => $possible_hosts,
					target   => $target, 
					port     => $port, 
					password => $password, 
				});
				$an->Log->entry({log_level => 3, message_key => "an_variables_0001", message_variables => {
					name1 => "node_db_id", value1 => $node_db_id, 
				}, file => $THIS_FILE, line => __LINE__});
				
				if ($node_db_id =~ /^\d+$/)
				{
					print $an->String->get({key => "scp_message_0022", variables => { node_name => $an->data->{sys}{anvil}{$node_key}{name} }});
					my $striker_rc = $an->Striker->update_striker_conf({
						variable => "scancore::db::${node_db_id}::password",
						value    => $an->data->{password}{to_set}{'striker.conf'},
						target   => $target, 
						port     => $port, 
						password => $password, 
					});
					if ($striker_rc)
					{
						# What?
						print $an->String->get({key => "scp_warning_0004", variables => { rc => $pgsql_rc }})."\n";
					}
					else
					{
						# 0 == goood.
						print $an->String->get({key => "state_0073"})."\n";
					}
				}
				else
				{
					# Failed to find our DB ID.
					print $an->String->get({key => "scp_warning_0007", variables => { rc => $pgsql_rc }})."\n";
				}
			}
			else
			{
				# Warn the user that this node is offline.
				print $an->String->get({key => "scp_warning_0006", variables => { rc => $pgsql_rc }})."\n";
			}
		}
	}
	
	# Done!
	print $an->String->get({key => "state_0136"})."\n";
	
	return(0);
}

# This looks al all known Striker peers and checks for access to all peers. It will store a list peers we 
# know of and their access information.
sub check_all_peers
{
	my ($an) = @_;
	$an->Log->entry({log_level => 3, title_key => "tools_log_0001", title_variables => { function => "check_all_nodes" }, message_key => "tools_log_0002", file => $THIS_FILE, line => __LINE__});
	
	my $offline = 0;
	$an->ScanCore->get_striker_peers();
	foreach my $peer (sort {$a cmp $b} keys %{$an->data->{sys}{dashboard}})
	{
		print $an->String->get({key => "scp_message_0023", variables => { peer_name => $peer }});
		
		$an->Log->entry({log_level => 3, message_key => "an_variables_0001", message_variables => {
			name1 => "peer", value1 => $peer, 
		}, file => $THIS_FILE, line => __LINE__});
		if ($an->data->{sys}{dashboard}{$peer}{online})
		{
			# Accessed!
			print $an->String->get({key => "scp_message_0010"})."\n";
		}
		else
		{
			# This node is offline. Warn the user.
			$offline = 1;
			$an->Log->entry({log_level => 3, message_key => "an_variables_0001", message_variables => {
				name1 => "offline", value1 => $offline, 
			}, file => $THIS_FILE, line => __LINE__});
			print $an->String->get({key => "scp_warning_0003"})."\n";
			print $an->String->get({key => "scp_warning_0005", variables => { peer => $peer }})."\n";
		}
	}
	
	$an->Log->entry({log_level => 3, message_key => "an_variables_0001", message_variables => {
		name1 => "offline", value1 => $offline, 
	}, file => $THIS_FILE, line => __LINE__});
	return($offline)
}

# This looks at all known Anvil! systems and checks for access to all nodes. Returns '1' is any are found to
# be offline.
sub check_all_nodes
{
	my ($an) = @_;
	$an->Log->entry({log_level => 3, title_key => "tools_log_0001", title_variables => { function => "check_all_nodes" }, message_key => "tools_log_0002", file => $THIS_FILE, line => __LINE__});
	
	my $offline = 0;
	
	# I'll want this hash later, so store it
	$an->data->{anvil_data} = $an->ScanCore->get_anvils();
	foreach my $hash_ref (@{$an->data->{anvil_data}})
	{
		my $this_anvil_uuid = $hash_ref->{anvil_uuid};
		my $this_anvil_name = $hash_ref->{anvil_name};
		$an->Log->entry({log_level => 3, message_key => "an_variables_0002", message_variables => {
			name1 => "this_anvil_uuid", value1 => $this_anvil_uuid, 
			name2 => "this_anvil_name", value2 => $this_anvil_name, 
		}, file => $THIS_FILE, line => __LINE__});
		
		print $an->String->get({key => "scp_message_0008", variables => { anvil_name => $this_anvil_name }})."\n";
		
		# Load this Anvil!
		$an->Striker->load_anvil({anvil_uuid => $this_anvil_uuid});
		
		# Quick scan both nodes to make sure we have access.
		foreach my $node_key ("node1", "node2")
		{
			# Tell the user we're going to try connecting
			print $an->String->get({key => "scp_message_0009", variables => { node_name => $an->data->{sys}{anvil}{$node_key}{name} }});
			
			$an->Striker->scan_node({uuid => $an->data->{sys}{anvil}{$node_key}{uuid}, short_scan => 1});
			
			# If either node is offline, error out. We don't care about '--force' here because 
			# they've asked us to change the passwords on an Anvil!, which requires both nodes 
			# be up.
			if ($an->data->{sys}{anvil}{$node_key}{online})
			{
				# Accessed!
				print $an->String->get({key => "scp_message_0010"})."\n";
			}
			else
			{
				# Failed. :(
				print $an->String->get({key => "scp_warning_0003"})."\n";
				$offline = 1;
				$an->Log->entry({log_level => 3, message_key => "an_variables_0001", message_variables => {
					name1 => "offline", value1 => $offline, 
				}, file => $THIS_FILE, line => __LINE__});
			}
		}
	}
	
	$an->Log->entry({log_level => 3, message_key => "an_variables_0001", message_variables => {
		name1 => "offline", value1 => $offline, 
	}, file => $THIS_FILE, line => __LINE__});
	return($offline);
}

# This looks for the anvil_uuid of the requested anvil.
sub find_anvil
{
	my ($an) = @_;
	$an->Log->entry({log_level => 3, title_key => "tools_log_0001", title_variables => { function => "find_anvil" }, message_key => "tools_log_0002", file => $THIS_FILE, line => __LINE__});
	
	my $anvil_data = $an->ScanCore->get_anvils();
	foreach my $hash_ref (@{$anvil_data})
	{
		my $this_anvil_uuid = $hash_ref->{anvil_uuid};
		my $this_anvil_name = $hash_ref->{anvil_name};
		$an->Log->entry({log_level => 3, message_key => "an_variables_0002", message_variables => {
			name1 => "this_anvil_uuid", value1 => $this_anvil_uuid, 
			name2 => "this_anvil_name", value2 => $this_anvil_name, 
		}, file => $THIS_FILE, line => __LINE__});
		
		if ($an->data->{switches}{anvil} eq $this_anvil_name)
		{
			print $an->String->get({key => "scp_message_0008", variables => { anvil_name => $this_anvil_name }})."\n";
			
			$an->data->{anvil}{uuid} = $this_anvil_uuid;
			$an->data->{anvil}{name} = $this_anvil_name;
			$an->Log->entry({log_level => 3, message_key => "an_variables_0001", message_variables => {
				name1 => "anvil::uuid", value1 => $an->data->{anvil}{uuid}, 
			}, file => $THIS_FILE, line => __LINE__});
			last;
		}
	}
	
	if ($an->data->{anvil}{uuid})
	{
		# Load the Anvil!
		$an->Striker->load_anvil({anvil_uuid => $an->data->{anvil}{uuid}});
		
		# Quick scan both nodes to make sure we have access.
		my $offline = 0;
		foreach my $node_key ("node1", "node2")
		{
			print $an->String->get({key => "scp_message_0009", variables => { node_name => $an->data->{sys}{anvil}{$node_key}{name} }});
			
			$an->Striker->scan_node({uuid => $an->data->{sys}{anvil}{$node_key}{uuid}, short_scan => 1});
			
			if ($an->data->{sys}{anvil}{$node_key}{online})
			{
				# Accessed!
				print $an->String->get({key => "scp_message_0010"})."\n";
			}
			else
			{
				# Failed. :(
				print $an->String->get({key => "scp_warning_0003"})."\n";
				$offline = 1;
				$an->Log->entry({log_level => 3, message_key => "an_variables_0001", message_variables => {
					name1 => "offline", value1 => $offline, 
				}, file => $THIS_FILE, line => __LINE__});
			}
		}
		
		# If either node is offline, error out. We don't care about '--force' here because they've 
		# asked us to change the passwords on an Anvil!, which requires both nodes be up.
		if ($offline)
		{
			# Can't proceed without both nodes being accessible
			print $an->String->get({key => "scp_error_0007"})."\n";
			$an->nice_exit({exit_code => 7});
		}
	}
	else
	{
		# Could not find this anvil.
		print $an->String->get({key => "scp_error_0008", variables => { anvil_name => $an->data->{switches}{anvil} }})."\n";
		$an->nice_exit({exit_code => 8});
	}
	
	return(0);
}

# This mangles the passwords to prepare them for setting in the various places.
sub prepare_passwords
{
	my ($an) = @_;
	$an->Log->entry({log_level => 3, title_key => "tools_log_0001", title_variables => { function => "prepare_passwords" }, message_key => "tools_log_0002", file => $THIS_FILE, line => __LINE__});
	
	# Is the password long enough?
	if (length($an->data->{switches}{password}) < $an->data->{password}{minimum_length})
	{
		if ($an->data->{switches}{force})
		{
			# Force requested.
			print $an->String->get({key => "scp_warning_0001"})."\n";
		}
		else
		{
			# Not forced, password is too short.
			print $an->String->get({key => "scp_error_0006"})."\n";
			$an->nice_exit({exit_code => 6});
		}
	}
	
	# We'll set all the passwords, then adjust as needed.
	$an->data->{password}{to_set}{apache}         = $an->data->{switches}{password};
	$an->data->{password}{to_set}{'cluster.conf'} = $an->data->{switches}{password};
	$an->data->{password}{to_set}{ipmi}           = $an->data->{switches}{password};
	$an->data->{password}{to_set}{scancore}       = $an->data->{switches}{password};
	$an->data->{password}{to_set}{'striker.conf'} = $an->data->{switches}{password};
	$an->data->{password}{to_set}{'system'}       = $an->data->{switches}{password};
	
	# We'll truncate IPMI to 16 characters.
	if (length($an->data->{password}{to_set}{ipmi}) > $an->data->{password}{'truncate'}{ipmi})
	{
		# Truncate (cluster.conf is used for IPMI)
		$an->Log->entry({log_level => 4, message_key => "an_variables_0002", message_variables => {
			name1 => "switches::password",       value1 => $an->data->{switches}{password}, 
			name2 => "password::truncate::ipmi", value2 => $an->data->{password}{'truncate'}{ipmi}, 
		}, file => $THIS_FILE, line => __LINE__});
		
		$an->data->{password}{to_set}{ipmi}           = substr($an->data->{switches}{password}, 0, $an->data->{password}{'truncate'}{ipmi});
		$an->data->{password}{to_set}{'cluster.conf'} = $an->data->{password}{to_set}{ipmi};
		$an->Log->entry({log_level => 4, message_key => "an_variables_0002", message_variables => {
			name1 => "password::to_set::ipmi",         value1 => $an->data->{password}{to_set}{ipmi}, 
			name2 => "password::to_set::cluster.conf", value2 => $an->data->{password}{to_set}{'cluster.conf'}, 
		}, file => $THIS_FILE, line => __LINE__});
		print $an->String->get({key => "scp_warning_0002", variables => { ipmi_password => $an->data->{password}{to_set}{ipmi} }})."\n";
	}
	
	# The password that will be written to cluster.conf for IPMI fencing needs to have some escapes 
	# (well, just one for now).
	$an->data->{password}{to_set}{'cluster.conf'} =~ s/'/&apos;/g;
	
	return(0);
}

# Print the usage information.
sub print_usage
{
	my ($an) = @_;
	$an->Log->entry({log_level => 3, title_key => "tools_log_0001", title_variables => { function => "print_usage" }, message_key => "tools_log_0002", file => $THIS_FILE, line => __LINE__});
	
	open (my $file_handle, ">", "/tmp/${THIS_FILE}.help") or die "Couldn't write help to /tmp/, error was: $!\n";
	print $file_handle $an->String->get({key => "scp_message_0001"})."\n";
	close $file_handle;
	
	system($an->data->{path}{'less'}." /tmp/${THIS_FILE}.help");

	return(0);
}
